import pprint
pp = pprint.PrettyPrinter(width=41, compact=True) 
import subprocess as sp
import tempfile
import yaml
import json
import string
import itertools
import time 
import re

# builds dictionary of dictionaries where first dict key is SRS 
# and second dict key are SRS properties
def metadata_builder(file, SRS_dict = {}, discrepancy = False):
	with open(file) as file:
		for line in file:
			if line[0] == '#':
				continue
			info = line.strip('\n').split('\t')
			if info[0] == 'sample_accession':
				continue
			SRS = info[0]
			if SRS not in SRS_dict:
				SRS_dict[SRS]={'SRR': [info[1]],
					    	  'paired':True if info[2]=='PAIRED' else False, 
					          'organism':info[3].replace(' ', '_'),
		            	      'tech':info[4],
						      'UMI':True if info[5].upper()=='YES' else False,
							  'Study': info[6]}
			else:
				# this is mostly for SRA having the 'paired' status wrong
				# don't want to hand-edit the main metadata file
				# so I think better to create a new file with
				# hand edited values for just the ones i want to change
				if discrepancy:
					runs = SRS_dict[SRS]['SRR']
					SRS_dict[SRS] = {'SRR':runs,
									 'paired':True if info[2]=='PAIRED' else False,
									 'organism':info[3],
									 'tech':info[4],
									 'UMI':True if info[5]=='YES' else False,
									 'Study': info[6]}
				else:
					runs = SRS_dict[SRS]['SRR']
					runs.append(info[1])
					SRS_dict[SRS]['SRR'] = runs
	return(SRS_dict)
def lookup_run_from_SRS(SRS, fqp):
	SRR_files=SRS_dict[SRS]['SRR']
	out = []
	for SRR in SRR_files:
		if SRS_dict[SRS]['paired']:
			#PE
			out.append(f'{fqp}/fastq/{SRR}_1.fastq.gz')
			out.append(f'{fqp}/fastq/{SRR}_2.fastq.gz')
		else:
			#SE
			out.append(f'{fqp}/fastq/{SRR}.fastq.gz')
	return(out)


def SRP_2_SRS(quant_path, SRP, reference, sfx, srs_dict):
	out=[]
	for sample in srs_dict.keys():
		tech = srs_dict[sample]['tech']
		if srs_dict[sample]['Study'] == SRP and tech in ['DropSeq', '10xv2', '10xv3']:
			out.append(f'{quant_path}/quant/{sample}/{tech}/{reference}/genecount/{sfx}')
	return out



def well_and_droplet_input(organism, reference, quant_path, SRP_droplet_dict, otd, sfx):
	target_srps = [ srp for srp in  SRP_droplet_dict.keys() if SRP_droplet_dict[srp]['organism'] == organism ] 
	if organism in droplet_only_org:
		out = [f'pipeline_data/clean_quant/{srp}/{reference}/{sfx}' for srp in target_srps]
	else:
		out = [f'{quant_path}/quant/{organism}/well/{reference}__counts.Rdata' ] + [f'pipeline_data/clean_quant/{srp}/{reference}/{sfx}' for srp in target_srps]	
	return(out)


def well_and_droplet_stats(organism, reference, quant_path, SRS_dict, otd):
	well = [f'{quant_path}/quant/{organism}/{tech}/{reference}__counts.Rdata' for tech in otd[organism]]
	droplet = [f'{quant_path}/quant/{srs}/{SRS_dict[srs]["tech"]}/{reference}/genecount/stats.tsv' for srs in organism_droplet_dict[organism]]
	return well + droplet


def REF_idx(organism, ref, org2tech):
	out = [f'references/velocity/{tech}/{ref}/tr2g.tsv' for tech in org2tech[organism]]
	return out



def ORG_ref(organism, which_return):
	if organism.lower() == 'mus_musculus':
		refs = ['mm-mus_musculus']
	elif organism.lower() == 'homo_sapiens':
		refs = ['hs-homo_sapiens']
	elif organism.lower() == 'macaca_fascicularis':
		refs = ['hs-homo_sapiens','mf-macaca_mulatta']
	elif organism.lower() == 'gallus_gallus':
		refs = ['gg-gallus_gallus']
	else:
		print(organism + ' NO MATCH')
		exit()

	if which_return == 'matrix':
		out = [f'pipeline_data/clean_quant/{organism}/{ref}_full_sparse_matrix.Rdata' for ref in refs] + [f'pipeline_data/clean_quant/{organism}/{ref}_full_sparse_unspliced_matrix.Rdata' for ref in refs]
	else:
		out = [f'pipeline_data/cell_info/{organism}_{ref}_cell_info.tsv' for ref in refs]

	return(out)

def get_whitelist_from_tech(tech):
	if tech == 'DropSeq':
		return f'references/whitelist/DropSeq/barcodes.txt'
	else:
		return('references/whitelist/10x/{tech}.txt')

def get_kallisto_quant_layout_flag(is_paired):
	if is_paired:
		return ''
	else: 
		return '--single -l 200 -s 30'

def DROPSEQ_samples_from_reference(quant_path ,srs_dict):
	out= []
	for sample in srs_dict.keys():
		if srs_dict[sample]['tech'] == 'DropSeq' :
			if srs_dict[sample]['organism'] == 'Homo_sapiens':
				out.append(f'{quant_path}/quant/{sample}/DropSeq/hs-homo_sapiens/output.sorted.bus')
			else:
				out.append(f'{quant_path}/quant/{sample}/DropSeq/mm-mus_musculus/output.sorted.bus')
	return out 

srr_sample_file = config['srr_sample_file']
SRS_dict = metadata_builder(srr_sample_file)
sample_names = list(SRS_dict.keys())

# build organism <-> SRS dict for nonUMI data
organism_well_dict = {}
organism_welltech_dict ={'Homo_sapiens':[], 'Mus_musculus':[], 'Macaca_fascicularis':[], 'Gallus_gallus':[] }
for x in SRS_dict:
	if not SRS_dict[x]['UMI']:# not UMI == Well
		organism = SRS_dict[x]['organism']
		tech = SRS_dict[x]['tech']
		if tech not in organism_welltech_dict[organism]:
			organism_welltech_dict[organism].append(tech)
		if organism not in organism_well_dict:
			organism_well_dict[organism] = [x]
		else:
			srs = organism_well_dict[organism]
			srs.append(x)
			organism_well_dict[organism] = srs
################

# build organsim <-> SRS dict for UMI/droplet data
organism_droplet_dict = {}
droplet_samples = set()
srp_droplet_dict = dict()
for x in SRS_dict:
	if SRS_dict[x]['UMI']:
		droplet_samples.add(x)
		organism = SRS_dict[x]['organism']
		study = SRS_dict[x]['Study']
		tech = SRS_dict[x]['tech']
		if study not in srp_droplet_dict:
			srp_droplet_dict[study] = {'tech':tech, 'organism':organism }
		if organism not in organism_droplet_dict:
			organism_droplet_dict[organism] = [x]
		else:
			srs = organism_droplet_dict[organism]
			srs.append(x)
			organism_droplet_dict[organism] = srs
################


git_dir = config['git_dir']
bustools_path = config['bustools_path']
working_dir = config['working_dir']
conda_dir = config['conda_dir']
fastq_path = config['fastq_path']
quant_path = config['quant_path']
config_abspath=config['config_abspath']
pattern_file=config['samplename_patterns']
SRS_UMI_samples = []
SRS_nonUMI_samples = []
for SRS in SRS_dict.keys():
	if SRS_dict[SRS]['UMI'] and SRS_dict[SRS]['paired']:
		SRS_UMI_samples.append(SRS)
	elif SRS_dict[SRS]['tech'] != 'BULK':
		SRS_nonUMI_samples.append(SRS)


organism = ['Mus_musculus', 'Macaca_fascicularis', 'Homo_sapiens', 'Gallus_gallus']
# macaque and chicken don't have well data
droplet_only_org = config['droplet_only_org'] # ['Macaca_fascicularis', 'Gallus_gallus']
# A is ~seuratCluster+batch+percent.mt and B is ~seuratCluster+batch+percent.mt+organism
report: "report.rst"

if config['subset_clustering'] == 'False':
	# partitions_to_run = ['universe', 'TabulaDroplet', 'onlyWELL']
	# scIB_partitions_to_run =  ['universe', 'TabulaDroplet'] 
	# Homo_sapiens/Mus_musculus are droplet only, species specific quantification
	partitions_to_run = [ 'TabulaDroplet', 'onlyWELL', "Homo_sapiens", "Mus_musculus"]
	scIB_partitions_to_run =  [ 'TabulaDroplet',"Homo_sapiens", "Mus_musculus"] 
else:
	partitions_to_run = ['celltype_subset']
	scIB_partitions_to_run = ['celltype_subset']


rule all:
	input:
		'pipeline_data/clean_quant_' + config['ambient'] + '/all_species_full_sparse_matrix.Rdata',
		#'pipeline_data/clean_quant_' + config['ambient'] + '/all_species_full_sparse_unspliced_matrix.Rdata',
		'pipeline_data/cell_info/all_cell_info.tsv',
		'pipeline_data/clean_quant_' + config['ambient'] + '/mito_counts.tsv'
	
# get annotation for each species 
rule make_sample_name_prefixs:
	input: srr_sample_file
	output: pattern_file
	run:
		patterns=set()
		for sample in sample_names:									
			int(sample[-1])#some hacky sample name checking - if the last character is not a digit(which it should bed ), 
			#this will fail 
			patterns.add(re.sub(r'\d+$', '', sample))
		patterns=list(patterns)
		with open(config['samplename_patterns'], 'w+') as infl:
			[infl.write(p +'\n') for p in patterns]

rule download_annotation:
	output:
		mouse_anno='references/gtf/mm-mus_musculus_anno.gtf.gz',
		#macaque_faca_anno='references/gtf/macaca_fascicularis_anno.gtf.gz',
		macaque_mul_anno= 'references/gtf/mf-macaca_mulatta_anno.gtf.gz',
		human_anno='references/gtf/hs-homo_sapiens_anno.gtf.gz',
		chick_anno='references/gtf/gg-gallus_gallus_anno.gtf.gz'
	shell:
		'''
		mkdir -p references
		wget -O {output.mouse_anno} ftp://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_mouse/release_M25/gencode.vM25.annotation.gtf.gz
		wget -O {output.macaque_mul_anno} ftp://ftp.ensembl.org/pub/release-101/gtf/macaca_mulatta/Macaca_mulatta.Mmul_10.101.gtf.gz
		wget -O {output.human_anno} ftp://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_35/gencode.v35.annotation.gtf.gz
		wget -O {output.chick_anno} http://ftp.ensembl.org/pub/release-104/gtf/gallus_gallus/Gallus_gallus.GRCg6a.104.gtf.gz
		'''
		#wget -O {output.macaque_faca_anno} ftp://ftp.ensembl.org/pub/release-98/gtf/macaca_fascicularis/Macaca_fascicularis.Macaca_fascicularis_5.0.98.gtf.gz



rule get_velocity_files:
	input: 
		gtf = 'references/gtf/{reference}_anno.gtf.gz'
	output:
		'references/velocity/{tech}/{reference}/cDNA_introns.fa',
		'references/velocity/{tech}/{reference}/tr2g.tsv'
	params:
		out_dir = lambda wildcards: f'references/velocity/{wildcards.tech}/{wildcards.reference}/'
	shell:
		'''
		module load R/3.6
		Rscript {git_dir}/src/get_velocity_annotation.R {input.gtf} {wildcards.reference} {wildcards.tech} {params.out_dir} {git_dir}
		'''

rule make_mitochondiral_gene_lists:
	input:
		mouse_anno = 'references/gtf/mm-mus_musculus_anno.gtf.gz',
		macaque_mul_anno = 'references/gtf/mf-macaca_mulatta_anno.gtf.gz',
		human_anno = 'references/gtf/hs-homo_sapiens_anno.gtf.gz',
		chick_anno = 'references/gtf/gg-gallus_gallus_anno.gtf.gz'
	output:
		mouse_mito = 'references/mito_genes/mm-mus_musculus_mitogenes.txt',
		macaque_mito = 'references/mito_genes/mf-macaca_mulatta_mitogenes.txt',
		human_mito = 'references/mito_genes/hs-homo_sapiens_mitogenes.txt',
		chick_anno = 'references/mito_genes/gg-gallus_gallus_mitogenes.txt'
	shell:
		'''
		module load R/4.0.3
		Rscript {git_dir}/src/make_mito_genelists.R {input} {output}
		'''

# need to make mouse, human, macaque
rule kallisto_index:
	input:
		'references/velocity/{tech}/{reference}/cDNA_introns.fa'
	output:
		'references/kallisto_idx/{tech}/{reference}.idx'
	shell:
		"""
		module load kallisto/0.46.2
		kallisto index {input} -i {output}
		"""

# get / make the bustool count tx file
# this does the pseudoalignment for UMI data (e.g. 10x)
rule kallisto_bus:
	input:
		fastq = lambda wildcards: lookup_run_from_SRS(wildcards.SRS, fastq_path),
		idx = 'references/kallisto_idx/{tech}/{reference}.idx'
	output:
		bus = quant_path + '/quant/{SRS}/{tech}/{reference}/output.bus',
		ec = quant_path + '/quant/{SRS}/{tech}/{reference}/matrix.ec',
		tx_name = quant_path + '/quant/{SRS}/{tech}/{reference}/transcripts.txt'
	threads: 8
	group: "bus"
	params:
		tech = lambda wildcards: SRS_dict[wildcards.SRS]['tech'],
		paired_flag = lambda wildcards: '' if SRS_dict[wildcards.SRS]['paired'] else '--single',
		out_dir = lambda wildcards:  f'{quant_path}/quant/{wildcards.SRS}/{wildcards.tech}/{wildcards.reference}'
	shell:
		'''
		module load kallisto/0.46.2
		kallisto bus {params.paired_flag} -t {threads} -x {params.tech} \
					-i {input.idx} -o {params.out_dir} {input.fastq}
		'''


# pseudoaligment for nonUMI data (e.g. smartseq)
rule kallisto_quant:
	input:
		fastq = lambda wildcards: lookup_run_from_SRS(wildcards.SRS, fastq_path),
		idx = 'references/kallisto_idx/well/{reference}.idx'
	output:
		quant = quant_path + '/quant/{SRS}/well/{reference}/abundance.tsv.gz'
	params:
		paired_flag = lambda wildcards: get_kallisto_quant_layout_flag(SRS_dict[wildcards.SRS]['paired']),
		outdir =lambda wildcards:  f'{quant_path}/quant/{wildcards.SRS}/well/{wildcards.reference}'
	threads: 2 
	group:'quant'
	shell:
		'''
		module load kallisto/0.46.2
		kallisto quant {params.paired_flag} -t {threads} --bias \
					-i {input.idx} -o {params.outdir} {input.fastq}
		gzip {params.outdir}/abundance.tsv
		'''

# sorting required for whitelist creation and correction
# make these temp files
rule bustools_sort:
	input:
		quant_path + '/quant/{SRS}/{tech}/{reference}/output.bus'
	output:
		temp(quant_path +'/quant/{SRS}/{tech}/{reference}/output.sorted.bus')
	threads: 8 
	group: "bus"
	shell:
		"""
		{bustools_path}/./bustools sort -t {threads} -m 100G \
			{input} \
			-o {output}
		"""

rule get_10x_whitelists:
	output:
		two = 'references/whitelist/10x/10xv2.txt',
		three = 'references/whitelist/10x/10xv3.txt'
	shell:
		"""
		wget -O {output.two} https://github.com/BUStools/getting_started/releases/download/velocity_tutorial/10xv2_whitelist.txt
		wget -O {output.three} https://github.com/BUStools/getting_started/releases/download/species_mixing/10xv3_whitelist.txt
		"""

rule make_dropseq_whitelists:
	input: 
		DROPSEQ_samples_from_reference(quant_path, SRS_dict)
	output:
		'references/whitelist/DropSeq/barcodes.txt'
	shell:
		'''
		mkdir -p dropseq_temp
		for sample in {input};
		do 
			temp=$(mktemp -p dropseq_temp/)
			{bustools_path}/./bustools whitelist --output $temp $sample 
		done 
		
		cat dropseq_temp/* | sort -u  >  {output}
		rm -rf dropseq_temp
		'''



# find barcodes, correct barcodes
# make these temp files
rule bustools_whitelist_correct_count:
	input:
		bus = quant_path + '/quant/{SRS}/{tech}/{reference}/output.sorted.bus',
		matrix = quant_path + '/quant/{SRS}/{tech}/{reference}/matrix.ec',
		tx_name = quant_path +'/quant/{SRS}/{tech}/{reference}/transcripts.txt',
		tx_map = 'references/velocity/{tech}/{reference}/tr2g.tsv',
		whitelist = lambda wildcards: get_whitelist_from_tech(wildcards.tech)
	output:		
		spliced = quant_path +'/quant/{SRS}/{tech}/{reference}/genecount/spliced.mtx', 
		unspliced = quant_path +'/quant/{SRS}/{tech}/{reference}/genecount/unspliced.mtx', 
	params:
		bus_out =  lambda wildcards: f'{quant_path}/quant/{wildcards.SRS}/{wildcards.tech}/{wildcards.reference}/genecount/',
		vref = lambda wildcards: f'references/velocity/{wildcards.tech}/{wildcards.reference}'
	shell:
		''' 
		
		{bustools_path}/./bustools capture -s -x -o {params.bus_out}/TMP.spliced.bus -c {params.vref}/introns_tx_to_capture.txt -e {input.matrix} -t {input.tx_name} {input.bus}
		{bustools_path}/./bustools capture -s -x -o {params.bus_out}/TMP.unspliced.bus -c {params.vref}/cDNA_tx_to_capture.txt -e {input.matrix} -t {input.tx_name} {input.bus}

		{bustools_path}/./bustools count -o {params.bus_out}/spliced -g {params.vref}/tr2g.tsv -e {input.matrix}  -t {input.tx_name}  --genecounts {params.bus_out}/TMP.spliced.bus
		{bustools_path}/./bustools count -o {params.bus_out}/unspliced -g {params.vref}/tr2g.tsv -e {input.matrix} -t {input.tx_name}  --genecounts {params.bus_out}/TMP.unspliced.bus
		rm {params.bus_out}/TMP*
		'''
	
	
# decontX (could) be run here, depending on what the yaml says
rule create_sparse_matrix:
	input:
		spliced = lambda wildcards:  SRP_2_SRS(quant_path, wildcards.srp, wildcards.reference, 'spliced.mtx', SRS_dict),
		unspliced = lambda wildcards:  SRP_2_SRS(quant_path, wildcards.srp, wildcards.reference, 'unspliced.mtx',SRS_dict),
		mito_genelist = 'references/mito_genes/{reference}_mitogenes.txt',
		pattern_file= pattern_file
	output:
		stats= 'pipeline_data/clean_quant/{srp}/{reference}/stats.tsv',
		pct_mt = 'pipeline_data/clean_quant/{srp}/{reference}/pct_mt.tsv',
		spliced_matrix = 'pipeline_data/clean_quant/{srp}/{reference}/matrix.Rdata',
		unspliced_matrix = 'pipeline_data/clean_quant/{srp}/{reference}/unspliced_matrix.Rdata'
	params:
		ambient = config['ambient'],
		matrix_outdir = lambda wildcards: f'pipeline_data/clean_quant/{wildcards.srp}/{wildcards.reference}/',
		bus_outdirs = lambda wildcards:  SRP_2_SRS(quant_path, wildcards.srp, wildcards.reference, '', SRS_dict),
	shell:
		"""
		module load R/4.0.3 #need 1.10 of droplet utils 
        Rscript {git_dir}/src/remove_empty_UMI_make_sparse_matrix.R {params.matrix_outdir} {input.mito_genelist} {pattern_file} {params.ambient} {git_dir} {params.bus_outdirs} 
		"""		
# spit out both intron and exon counts 

rule merge_nonUMI_quant_by_organism:
	input:
		quant = lambda wildcards: expand(quant_path + '/quant/{SRS}/well/{{reference}}/abundance.tsv.gz', 
										SRS = [srs for srs in SRS_dict.keys() if SRS_dict[srs]['organism'] == wildcards.organism and SRS_dict[srs]['tech'] not in ['DropSeq', '10xv1', '10xv2', '10xv3']]),
		tx_map = 'references/velocity/well/{reference}/tr2g.tsv',
		gtf = 'references/gtf/{reference}_anno.gtf.gz'
	output:
		quant_path + '/quant/{organism}/well/{reference}__counts.Rdata',
		quant_path + '/quant/{organism}/well/{reference}__counts_tx.Rdata'
	shell:
		"""
		module load R/3.6
		Rscript {git_dir}/src/merge_nonUMI_quant_by_organism.R {output} {input.tx_map} {input.gtf} {quant_path} {wildcards.reference}
		"""

rule combine_well_and_umi:
	input:
		srr_metadata = config['srr_sample_file'],
		gtf='references/gtf/{reference}_anno.gtf.gz',
		spliced_counts = lambda wildcards: well_and_droplet_input(wildcards.organism, wildcards.reference, 
																quant_path, srp_droplet_dict, organism_welltech_dict, 'matrix.Rdata'),
		intron_counts = lambda wildcards: well_and_droplet_input(wildcards.organism, wildcards.reference, quant_path, 			
																srp_droplet_dict, organism_welltech_dict, 'unspliced_matrix.Rdata')

	output:
		cell_info = 'pipeline_data/cell_info/{organism}_{reference}_cell_info.tsv',
		spliced_matrix = 'pipeline_data/clean_quant/{organism}/{reference}_full_sparse_matrix.Rdata',
		unspliced_matrix = 'pipeline_data/clean_quant/{organism}/{reference}_full_sparse_unspliced_matrix.Rdata'
		  # note that this is the quant in the LOCAL directory
	shell:
		"""
		module load R/3.6
		echo {wildcards.organism}
		echo {wildcards.reference}
		echo {input.spliced_counts}
		Rscript {git_dir}/src/build_sparse_matrix.R {wildcards.organism} {output} {input.srr_metadata} {input.gtf} {input.spliced_counts}
		"""

rule mito_stats:
	input:
		human_hs_quant = well_and_droplet_input('Homo_sapiens', 'hs-homo_sapiens',quant_path, srp_droplet_dict, organism_welltech_dict, 'pct_mt.tsv'),
		mouse_mm_quant = well_and_droplet_input('Mus_musculus', 'mm-mus_musculus',quant_path, srp_droplet_dict, organism_welltech_dict, 'pct_mt.tsv'),
		monkey_mf_quant = well_and_droplet_input('Macaca_fascicularis', 'mf-macaca_mulatta', quant_path, srp_droplet_dict, organism_welltech_dict, 'pct_mt.tsv'),
		monkey_hs_quant = well_and_droplet_input('Macaca_fascicularis', 'hs-homo_sapiens', quant_path, srp_droplet_dict, organism_welltech_dict, 'pct_mt.tsv'),
		pattern_file=pattern_file
	output:
		'pipeline_data/clean_quant/mito_counts.tsv'
	params:
		drop_mt_pfx = 'pct_mt.tsv',
		mito_gene_stem = '_mitogenes.txt'
	shell:
		"""
		module load R/4.0
		Rscript {git_dir}/src/mito_stats.R {working_dir} {params.drop_mt_pfx} {quant_path} {params.mito_gene_stem} {pattern_file} {output}
		"""

rule merge_across_references:
	input:
		srr_metadata = config['srr_sample_file'],
		matrix = [ORG_ref(organism, 'matrix') for organism in ['Mus_musculus', 'Macaca_fascicularis', 'Homo_sapiens', 'Gallus_gallus'] ],
		cell_info = [ORG_ref(organism, 'cell_info') for organism in ['Mus_musculus', 'Macaca_fascicularis', 'Homo_sapiens', 'Gallus_gallus'] ],
		pattern_file =pattern_file
	output:
		'pipeline_data/clean_quant/all_species_full_sparse_matrix.Rdata',
		'pipeline_data/clean_quant/all_species_full_sparse_unspliced_matrix.Rdata',
		'pipeline_data/cell_info/all_cell_info.tsv',
		'references/ENSG2gene_name.tsv.gz',
		expand('pipeline_data/clean_quant/{species}/full_sparse_matrix.Rdata',species= ['Homo_sapiens', 'Mus_musculus', 'Gallus_gallus']),
		expand('pipeline_data/clean_quant/{species}/full_sparse_unspliced_matrix.Rdata',species= ['Homo_sapiens', 'Mus_musculus', 'Gallus_gallus']),
		'pipeline_data/clean_quant/droplet_quant_stats.tsv'
	shell:
		"""
		module load R/3.6
		Rscript {git_dir}/src/blend_macaque_merge_across_reference.R {working_dir} {git_dir} {input.srr_metadata} {pattern_file}
		"""

localrules: mv_clean_quant
rule mv_clean_quant:
	input:
		'pipeline_data/clean_quant/mito_counts.tsv',
		'pipeline_data/clean_quant/all_species_full_sparse_matrix.Rdata',
	output:
		'pipeline_data/clean_quant_' + config['ambient'] + '/mito_counts.tsv',
		'pipeline_data/clean_quant_' + config['ambient'] + '/all_species_full_sparse_matrix.Rdata'
	params:
		ambient = config['ambient']
	shell:
		"""
		#mv pipeline_data/clean_quant/ pipeline_data/clean_quant_{params.ambient} 
		"""
